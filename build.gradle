buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.elasticsearch.gradle:build-tools:${elastic_version}"
    }
}

group = 'org.elasticsearch.plugin'
version = "${plugin_version}"

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'elasticsearch.esplugin'
apply plugin: 'elasticsearch.yaml-rest-test'

// license of this project
licenseFile = rootProject.file('LICENSE')
noticeFile = rootProject.file('README.md')

publishing {
    publications {
        // add license information to generated poms
        all {
            pom.withXml { XmlProvider xml ->
                Node node = xml.asNode()

                Node license = node.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The MIT License (MIT)')
                license.appendNode('url', './LICENSE')
                license.appendNode('distribution', 'repo')

                Node developer = node.appendNode('developers').appendNode('developer')
                developer.appendNode('name', 'Opendatasoft')
                developer.appendNode('url', 'https://www.opendatasoft.com/')
            }
        }
    }
}

esplugin {
    name 'elasticsearch-plugin-geoshape'
    description 'Add extra geo capabilities to default elastic geo_shape data type.'
    classname 'org.opendatasoft.elasticsearch.plugin.GeoExtensionPlugin'
    licenseFile = rootProject.file('LICENSE')
    noticeFile = rootProject.file('README.md')
}


// dependencies for production but also test code
dependencies {
//    implementation 'org.jetbrains:annotations:20.1.0'
    compile "org.elasticsearch:elasticsearch:${elastic_version}"
    // required for ESClientYamlSuiteTestCase; fixes "RestApiYamlIT > initializationError"
    yamlRestTestImplementation "org.apache.logging.log4j:log4j-core:${versions.log4j}"

    // fixme: uncomment
//    // jts-io-common compile dependency
//    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
//
//    // WARNING: jts-io-common is needed for org.locationtech.jts.io.geojson
//    //   its version has to to be identical to the elastic:jts one (here: 1.15.0)
//    compile group: 'org.locationtech.jts.io', name: 'jts-io-common', version: '1.15.0'

}

test.enabled = false

// Skip license headers check
licenseHeaders.enabled = false

// Use elasticsearch checkstyle rules
checkstyleTest.enabled = true

dependencyLicenses.enabled = false

thirdPartyAudit.enabled = true

// fixes "Could not find org.elasticsearch.test:logger-usage:7.10.2"
//  see https://github.com/elastic/elasticsearch/issues/65247
loggerUsageCheck.enabled = false
